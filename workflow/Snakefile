import os
import sys
import pandas as pd

include: "rules/common.smk"

SAMPLES = list(samples['sample'])

base_dir = config['base_dir']

#running Snakemake with:
# snakemake --cores 1 --use-envmodules -n --use-singularity --singularity-args '-B /data/millerv2'

rule all:
    input:
        expand(os.path.join(base_dir,"fastqc_raw","{sample}_fastqc.html"),sample=SAMPLES),
        expand(os.path.join(base_dir,"fastqc_raw","{sample}_fastqc.zip"),sample=SAMPLES),
        expand(os.path.join(base_dir,"trimmed_reads","{sample}_trimmed.fastq"),sample=SAMPLES),
        os.path.join(base_dir,"Nanoplot","NanoPlot-report.html"),
        expand(os.path.join(base_dir,"fastqc_trimmed","{sample}_trimmed_fastqc.html"),sample=SAMPLES),
        expand(os.path.join(base_dir,"fastqc_trimmed","{sample}_trimmed_fastqc.zip"),sample=SAMPLES),
        expand(os.path.join(base_dir,"genome_alignmments","{sample}_alignment.sam"),sample=SAMPLES)


rule fastqc_raw:
    input:
        os.path.join(base_dir,"samples","{sample}.fastq")
    output:
        os.path.join(base_dir,"fastqc_raw","{sample}_fastqc.html"),
        os.path.join(base_dir,"fastqc_raw","{sample}_fastqc.zip")
    params:
        OUT_DIR = os.path.join(base_dir,"fastqc_raw")
    log:
        os.path.join(base_dir,"logs","{sample}_fastqc_raw.log")
    message:
        "Running fastqc_raw with {input}"
    envmodules:
        "fastqc/0.11.9"
    shell:
        "fastqc -o {params.OUT_DIR} {input} &> {log} "
    

rule nanoplot:
    input:
        expand(os.path.join(base_dir,"samples","{sample}.fastq"),sample=SAMPLES)
    container:
        "docker://staphb/nanoplot:1.33.0"
# This container defines the underlying OS for each job when using the workflow
# with --use-singularity
    output:
        os.path.join(base_dir,"Nanoplot","Dynamic_Histogram_Read_length.html"),
        os.path.join(base_dir,"Nanoplot","Dynamic_Histogram_Read_length.png"),
        os.path.join(base_dir,"Nanoplot","HistogramReadlength.png"),
        os.path.join(base_dir,"Nanoplot","LengthvsQualityScatterPlot_dot.png"),
        os.path.join(base_dir,"Nanoplot","LengthvsQualityScatterPlot_kde.png"),
        os.path.join(base_dir,"Nanoplot","LogTransformed_HistogramReadlength.png"),
        os.path.join(base_dir,"Nanoplot","NanoPlot-report.html"),
        os.path.join(base_dir,"Nanoplot","NanoStats.txt"),
        os.path.join(base_dir,"Nanoplot","Weighted_HistogramReadlength.png"),
        os.path.join(base_dir,"Nanoplot","Weighted_LogTransformed_HistogramReadlength.png"),
        os.path.join(base_dir,"Nanoplot","Yield_By_Length.png")
    params:
        OUT_DIR = os.path.join(base_dir,"Nanoplot")
    log:
        os.path.join(base_dir,"logs","Nanoplot.log")
    message:
        "Running Nanoplot with {input}"
    shell:'''
    NanoPlot -o {params.OUT_DIR} --fastq {input} 
    '''

#note, Nanofilt requires unzipped fq files

rule nanofilt:
    input:
        os.path.join(base_dir,"samples","{sample}.fastq") 
    output:
        os.path.join(base_dir,"trimmed_reads","{sample}_trimmed.fastq") 
    container:
        "docker://mcfonsecalab/nanofilt:latest"
    params:
        OUT_DIR = os.path.join(base_dir,"trimmed_reads"),
        headcrop = config['headcrop'],
        length = config['length']
    log:
        os.path.join(base_dir,"logs","{sample}_Nanofilt.log")
    message:
        "Running Nanofilt with {input}"
    shell:'''
    NanoFilt -l {params.length} --headcrop {params.headcrop} < {input} > {output}
    '''  

rule fastqc_trimmed:
    input:
        os.path.join(base_dir,"trimmed_reads","{sample}_trimmed.fastq")
    output:
        os.path.join(base_dir,"fastqc_trimmed","{sample}_trimmed_fastqc.html"),
        os.path.join(base_dir,"fastqc_trimmed","{sample}_trimmed_fastqc.zip")
    envmodules:
        "fastqc/0.11.9"
    params:
        OUT_DIR = os.path.join(base_dir,"fastqc_trimmed")
    shell:'''
    fastqc -o {params.OUT_DIR} {input}
    '''  
  
rule minimap_alignment:
    input:
        ref = config['genome_fasta'],
        fastq = os.path.join(base_dir,"samples","{sample}.fastq")
    output:
        os.path.join(base_dir,"genome_alignmments","{sample}_alignment.sam")
    #params:
    #    OUT_DIR = os.path.join(base_dir,"genome_alignments")
    log:
        os.path.join(base_dir,"logs","{sample}_minimap2.log")
    message:
        "Running minimap with {input} with {threads} threads"
    envmodules:
        "minimap2/2.20"
    threads:
        12
    shell:'''
    minimap2 -t {threads} -ax splice -uf -k14 {input.ref} {input.fastq} > {output}
    '''
    
    
